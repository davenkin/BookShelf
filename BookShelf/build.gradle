apply plugin: 'idea'
apply plugin: 'jetty'

[jettyRun, jettyRunWar, jettyStop]*.stopPort = 8081
[jettyRun, jettyRunWar, jettyStop]*.stopKey = 'stopKey'

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

repositories {
    mavenCentral()
}

sourceSets {
    acceptanceTest
}

configurations {
    acceptanceTestCompile.extendsFrom testCompile
    acceptanceTestRuntime.extendsFrom testRuntime
}


dependencies {
    compile 'javax.servlet:servlet-api:2.5'
    compile 'com.google.guava:guava:18.0'
    compile 'org.hsqldb:hsqldb:2.3.0'
    compile 'org.apache.commons:commons-dbcp2:2.0'
    compile 'org.springframework:spring-context:4.0.5.RELEASE'
    testCompile 'junit:junit:4.11'
    acceptanceTestCompile 'org.seleniumhq.selenium:selenium-java:2.42.0'
}


task acceptanceTest(type: Test) {
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath + sourceSets.main.runtimeClasspath

    doFirst {
        jettyRun.daemon = true
        jettyRun.execute()
    }

    doLast {
        jettyStop.execute()
    }
}

check.dependsOn acceptanceTest

idea {
    module {
        testSourceDirs += file('src/acceptanceTest/java')
        testSourceDirs += file('src/acceptanceTest/resources')
        scopes.TEST.plus += configurations.acceptanceTestCompile
        scopes.TEST.plus += configurations.acceptanceTestRuntime
    }
}
